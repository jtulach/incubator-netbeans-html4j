buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.netbeans.html:html4j-maven-plugin:1.7"
    }
}

plugins {
    id "me.tatarka.retrolambda" version "3.7.1"
    id 'org.openjfx.javafxplugin' version '0.0.7' apply false
}

apply plugin: 'java'
apply plugin: 'html4j'

java {
   disableAutoTargetJvm()
}

group 'cz.xelfi.demo'

allprojects {
    repositories {
        mavenCentral()
    }
    
    task checkJavaVersion {
        doLast {
            if (!System.getProperty("java.version").startsWith("11")) {
                throw new IllegalStateException("Please use JDK 11, that one is known to work with Gradle as well as libraries and plugins!");
            }
        }
    }

    compileJava.dependsOn(checkJavaVersion)
}

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

dependencies {
    compile project(":api")
    compile "org.netbeans.html:net.java.html.boot:1.7"
    compile "org.netbeans.html:net.java.html.json:1.7"
    compile "com.dukescript.api:javafx.base:8.60.11"
    compile "com.dukescript.api:javafx.beaninfo:0.6"
    compileOnly "org.netbeans.api:org-openide-util-lookup:RELEASE120-u1"
    annotationProcessor "org.netbeans.api:org-openide-util-lookup:RELEASE120-u1"
    annotationProcessor "com.dukescript.api:javafx.beaninfo:0.6"
    annotationProcessor "org.netbeans.html:net.java.html.boot:1.7"
    annotationProcessor "org.netbeans.html:net.java.html.json:1.7"
    testCompile "com.dukescript.api:junit-browser-runner:1.0"
    testRuntime "org.netbeans.html:net.java.html.boot.fx:1.7"
    testAnnotationProcessor "org.netbeans.html:net.java.html.boot:1.7"
}

task copyReactResources(type: Copy) {
    from 'src/main/resources' into 'build/retrolambda/main/';
}

compileJava.dependsOn(copyReactResources);
